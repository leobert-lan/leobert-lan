plugins {
  id 'org.jetbrains.kotlin.jvm' version "1.6.10"
  id 'org.jetbrains.kotlin.kapt' version "1.6.10"
  id 'com.google.devtools.ksp' version "1.6.10-1.0.2"
  id 'application'
  id 'com.diffplug.spotless' version "6.1.0"
  id "com.github.ben-manes.versions" version "0.39.0"
  id 'dev.zacsweers.moshix' version '0.16.0'
}

repositories {
  mavenCentral()
}

moshi {
  enableSealed = true
}

def compileArgs = ['-Xjsr305=strict', '-progressive', '-Xopt-in=kotlin.ExperimentalStdlibApi']

compileKotlin {
  kotlinOptions {
    jvmTarget = "17"
    freeCompilerArgs += compileArgs
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "17"
    freeCompilerArgs += compileArgs
  }
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(17))
  }
}

sourceSets {
  main {
    java {
      srcDir("build/generated/source/kapt/main")
    }
  }
}

mainClassName = 'io.leobert.UpdateReadmeKt'

// Fat jar configuration to run this as a standalone jar
// Configuration borrowed from https://stackoverflow.com/a/49284432/3323598
jar {
  manifest {
    attributes(
        'Main-Class': 'io.leobert.UpdateReadmeKt'
    )
  }
  from {
    configurations.compileClasspath.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

dependencies {

  implementation "com.github.ajalt:clikt:2.8.0"
  implementation 'com.google.code.gson:gson:2.8.5'
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
  implementation 'com.squareup.okio:okio:3.0.0'
  implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
  implementation "com.squareup.retrofit2:retrofit:2.9.0"
  implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
  implementation "com.squareup.retrofit2:converter-gson:2.9.0"
  implementation "com.squareup.moshi:moshi:1.13.0"
  implementation "com.squareup.moshi:moshi-adapters:1.13.0"

  // XML serialization
//  kapt 'com.tickaroo.tikxml:processor:0.8.13'
//  implementation 'com.tickaroo.tikxml:annotation:0.8.13'
//  implementation 'com.tickaroo.tikxml:core:0.8.13'
//  implementation 'com.tickaroo.tikxml:retrofit-converter:0.8.13'
//  implementation 'com.tickaroo.tikxml:converter-htmlescape:0.8.13'
}
